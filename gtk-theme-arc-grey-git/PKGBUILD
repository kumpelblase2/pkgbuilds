# Maintainer: tim hagemann <tim {at} eternalwings {dot} de>

pkgname=gtk-theme-arc-grey-git
_pkgname=arc-grey-theme
_pkgauthor=kumpelblase2
pkgver=594.b0d095f
pkgrel=1
pkgdesc="A flat theme with transparent elements for GTK 3, GTK 2 and Gnome-Shell. Latest commit from the master branch on Github."
arch=('any')
url="https://github.com/eti1337/${_pkgname}"
license=('GPL3')
depends=('gtk3' 'gtk-engine-murrine')
makedepends=('git')
source=(${_pkgname}::"git+${url}.git")
sha256sums=('SKIP')
conflicts=('gtk-theme-arc-grey')
provides=('gtk-theme-arc-grey')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
	cd "${srcdir}/${_pkgname}"
	./autogen.sh --prefix=/usr
}

package() {
	make -C "${srcdir}/${_pkgname}" DESTDIR="${pkgdir}" install
}
